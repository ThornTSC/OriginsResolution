// Adjust sprites drawn outside of VRAM
function bool Standalone.onWriteToSpriteTable(s16 px, s16 py, u16 renderQueue)
{
	bool prioFlag = (objA0.sprite_attributes & sprite_attribute.PRIORITY) != 0

	// Title card red bar
	if (objA0.update_address == addressof(TitleCard.RedBarElement.Update))
	{
		Renderer.drawCustomSprite("titlecard_redbar", px - 0x20, py - 0x40, 0x00, SPRITE_FLAG_PRIO | SPRITE_FLAG_NO_GLOBAL_TINT, renderQueue)

        // Draw additional red to reach the top of the screen. All other parts of the title card are positioned properly by adjusting their actual y positions.
		Renderer.drawCustomSprite("titlecard_redbar", px - 0x20, py - 0x40 - (getScreenHeightExtend()/2), 0x00, SPRITE_FLAG_PRIO | SPRITE_FLAG_NO_GLOBAL_TINT, renderQueue - 1)

		return true
	}

    // Characters in special stage
    if (objA0.update_address == 0x00903e || objA0.update_address == 0x00927a)
    {
        u8 character = (objA0.update_address == 0x00927a) ? getSecondCharacter() : getMainCharacter()
        u8 atex = (character == CHARACTER_TAILS) ? 0x10 : 0x00
        u64 key = stringformat(stringformat("%s_0x%02x", getCharacterBluesphereSpriteKey(character)), objA0.animation.sprite)
        if (Renderer.hasCustomSprite(key))
        {
            // Check if there's also a custom palette
            string paletteKey = getCharacterBluespherePaletteKey(character)
            if (System.hasExternalPaletteData(paletteKey, 0))
            {
                atex = 0x40 + character * 0x20
            }
        }
        else
        {
            if (character == CHARACTER_SONIC)
                key = Renderer.setupCustomCharacterSprite(0x0aaa7c, 0x0abe14, objA0.mapping_offset, objA0.animation.sprite, atex)
            else if (character == CHARACTER_TAILS)
                key = Renderer.setupCustomCharacterSprite(0x28f95a, 0x2908d2, objA0.mapping_offset, objA0.animation.sprite, atex)
            else
                key = Renderer.setupCustomCharacterSprite(0x0abf22, 0x0ad31a, objA0.mapping_offset, objA0.animation.sprite, atex)

            if (ROMDataAnalyser.isEnabled())
                Renderer.extractCustomSprite(key, getCharacterBluesphereSpriteKey(character), objA0.animation.sprite, atex)
        }
        Renderer.drawCustomSprite(key, px, py + getScreenHeightExtend(), atex, prioFlag ? SPRITE_FLAG_PRIO : 0, renderQueue)
        return true
    }

    // Tails' tails object in special stage
    if (objA0.update_address == 0x009488)
    {
        u8 atex = 0x10
        u64 key = stringformat(stringformat("%s_tails_0x%02x", getCharacterBluesphereSpriteKey(CHARACTER_TAILS)), objA0.animation.sprite)
        if (Renderer.hasCustomSprite(key))
        {
            // Check if there's also a custom palette
            string paletteKey = getCharacterBluespherePaletteKey(CHARACTER_TAILS)
            if (System.hasExternalPaletteData(paletteKey, 0))
            {
                atex = 0x40 + CHARACTER_TAILS * 0x20
            }
        }
        else
        {
            key = Renderer.setupCustomCharacterSprite(0x2909e8, 0x291106, objA0.mapping_offset, objA0.animation.sprite, atex)

            if (ROMDataAnalyser.isEnabled())
                Renderer.extractCustomSprite(key, stringformat("%s_tails", getCharacterBluesphereSpriteKey(CHARACTER_TAILS)), objA0.animation.sprite, atex)
        }
        Renderer.drawCustomSprite(key, px, py + getScreenHeightExtend(), atex, prioFlag ? SPRITE_FLAG_PRIO : 0, renderQueue)
        return true
    }

    // LBZ2 girder when approaching the Death Egg. Only drawn as an image until the graphics are placed in VRAM.
    if (objA0.update_address == 0x062aee && camera.position.x.u16 <= 0x3bc0)
    {
        Renderer.drawCustomSprite("lbz2_girder", px, py, 0x00, prioFlag ? SPRITE_FLAG_PRIO : 0, renderQueue)
        return true
    }

    // DEZ2 final boss robot and attached parts
    if (objA0.update_address == 0x07fd68)
    {
        if (objA0.base_state < 0x0c)
        {
            Renderer.drawCustomSprite("dez2boss_side", objA0.position.x.u16, objA0.position.y.u16 - camera.screenshake.offset + 3*getScreenHeightExtend()/2, 0x00, SPRITE_FLAG_WORLDSPACE, objA0.base_state < 0x08 ? 0xa002 : 0x2002)
            Renderer.drawCustomSprite("dez2boss_masteremeraldhatchcover", objA0.position.x.u16 + 0x7c, objA0.position.y.u16 + 0x34 - camera.screenshake.offset, 0x00, SPRITE_FLAG_WORLDSPACE, objA0.base_state < 0x08 ? 0xa003 : 0x2003)
            if (objA0.base_state > 0x06)
                Renderer.drawCustomSprite("dez2boss_cockpitdoor", objA0.position.x.u16 - 0x30, objA0.position.y.u16 - 0x56 - camera.screenshake.offset, 0x00, SPRITE_FLAG_WORLDSPACE, 0x2001)
        }
        else if (objA0.base_state >= 0x0c && objA0.base_state <= 0x12)
        {
            Renderer.drawCustomSprite("dez2boss_front", objA0.position.x.u16, objA0.position.y.u16 - camera.screenshake.offset, 0x00, SPRITE_FLAG_WORLDSPACE, 0x2001)
        }
        else // if (objA0.base_state >= 0x14)
        {
            Renderer.drawCustomSprite("dez2boss_side", objA0.position.x.u16, objA0.position.y.u16 - camera.screenshake.offset + 5*getScreenHeightExtend()/2, 0x00, SPRITE_FLAG_WORLDSPACE, 0x2001)
            if (u8[0xfffffaa9] == 0)
                Renderer.drawCustomSprite("dez2boss_masteremeraldhatchcover", objA0.position.x.u16 + 0x7c, objA0.position.y.u16 + 0x34 - camera.screenshake.offset, 0x00, SPRITE_FLAG_WORLDSPACE, 0x2002)
        }
        return true
    }

    // DEZ2 final boss robot while it is defeated and falling
    if (objA0.update_address == 0x080102)
    {
        Renderer.drawCustomSprite("dez2boss_side", objA0.position.x.u16, objA0.position.y.u16 - camera.screenshake.offset + 5*getScreenHeightExtend()/2, 0x00, SPRITE_FLAG_WORLDSPACE, 0x2001)
	    Renderer.drawCustomSprite("dez2boss_masteremeraldhatchcover", objA0.position.x.u16 + 0x7c, objA0.position.y.u16 + 0x70 - camera.screenshake.offset, 0x00, SPRITE_FLAG_WORLDSPACE, 0x2002)
	    Renderer.drawCustomSprite("dez2boss_cockpitdoor", objA0.position.x.u16 - 0x30, objA0.position.y.u16 - 0x46 - camera.screenshake.offset, 0x00, SPRITE_FLAG_WORLDSPACE, 0x2001)
        return true
    }

    // DEZ2 final boss hatch cover for Master Emerald (when not covering it). The first three addresses here are for sliding down, staying down, and sliding up, respectively.
    // The final address is that of UnloadObject, which the hatch cover controller uses in its final frame. We use unique data in the object to identify it.
    if (objA0.update_address == 0x080634 || objA0.update_address == 0x08067a || objA0.update_address == 0x0806a6 || (global.zone_act == 0x1700 && objA0.update_address == 0x01abb6 && u32[A0 + 0x42] == 0x007c0028))
    {
        if (objA0.update_address == 0x08067a)
            py -= 4
        Renderer.drawCustomSprite("dez2boss_masteremeraldhatchcover", px, py - camera.screenshake.offset, 0x00, 0, 0x9e00)
        return true
    }

    return base.Standalone.onWriteToSpriteTable(px, py, renderQueue)
}

// Prevent some minor pop-in for SSZ clouds when screen width is greater than 400px.
function bool Standalone.onDrawVdpSpriteCompound(s16 px, s16 py, u8 size, u16 index, u16 renderQueue, u8 spriteCounter)
{
	// SSZ clouds effect
	if (objA0.update_address == 0x057bf6)
	{
		Renderer.drawVdpSpriteWithAlpha(px,   py, size, index, 0xd000, 140)
		Renderer.drawVdpSpriteWithAlpha(px+1, py, size, index, 0xd000, 144)

		if (getScreenWidth() > 400)
		{
			// Draw an additional copy of the cloud one loop further to the right
			Renderer.drawVdpSpriteWithAlpha(px+512, py, size, index, 0xd000, 140)
			Renderer.drawVdpSpriteWithAlpha(px+513, py, size, index, 0xd000, 144)
		}

		return true
	}

	return base.Standalone.onDrawVdpSpriteCompound(px, py, size, index, renderQueue, spriteCounter)
}
