// Center the player character based on the screen height.
//# address-hook(0x01c11e) end(0x01c236)
function void UpdateCameraPositionY()
{
	char.position.y.u16 -= getScreenHeightExtend()/2
	base.UpdateCameraPositionY()
	char.position.y.u16 += getScreenHeightExtend()/2
}

// Start the camera centered as in the previous function.
// Also add various initial camera boundary adjustments for altered screen heights.
//# address-hook(0x01be46) end(0x01bfae)
function void SetupCharacterAtStartPosition()
{
	base.SetupCharacterAtStartPosition()
	if (checkpoint.number != 0 || isSonicIntro())
	{
		camera.position.y.u16 = max(camera.position.y.u16 - getScreenHeightExtend()/2, 0)
		camera.position.y.u16.player2 = max(camera.position.y.u16.player2 - getScreenHeightExtend()/2, 0)
		return
	}

	if (global.zone_act == 0x0000 && !isSonicIntro() && checkpoint.number == 0 && Game.getSetting(SETTING_LEVELLAYOUTS) == 2 && (isMainCharacter(CHARACTER_KNUCKLES) && (Game.getSetting(SETTING_AIZ_INTRO_KNUCKLES) != 0 || Game.isTimeAttack())))
	{
		move_area.bottom.current -= getScreenHeightExtend()
		move_area.bottom.target -= getScreenHeightExtend()
	}
	else if (global.zone == 0x03 || global.zone == 0x04 || global.zone_act == 0x0501 || global.zone_act == 0x0601 || global.zone_act == 0x0800 || global.zone == 0x09 || global.zone == 0x0b || global.zone == 0x13 || global.zone == 0x16)
	{
		move_area.bottom.current -= getScreenHeightExtend()
		move_area.bottom.target -= getScreenHeightExtend()
	}
	else if (global.zone_act == 0x0a01)
	{
		move_area.bottom.current -= getScreenHeightExtend()/2
		move_area.bottom.target -= getScreenHeightExtend()/2
	}
	else if (global.zone_act == 0x1701)
	{
		// Hidden Palace Zone via giant ring
		level.vertical_wrap -= getScreenHeightExtend()
		move_area.bottom.current -= getScreenHeightExtend()
		move_area.bottom.target -= getScreenHeightExtend()
	}

	camera.position.y.u16 = max(camera.position.y.u16 - getScreenHeightExtend()/2, 0)
	camera.position.y.u16.player2 = max(camera.position.y.u16.player2 - getScreenHeightExtend()/2, 0)
}

// When finishing an Act 1 boss and setting new Act 2 camera boundaries,
// adjust the lower boundary for altered screen heights.
//# address-hook(0x085da8) end(0x085de4)
function void fn085da8()
{
	if (global.zone == 0x08)
		return

	A1 = 0x01bcce + (global.zone * 16)
	screenmover_target.left = u16[(A1+=2)-2]
	screenmover_target.right = u16[(A1+=2)-2]
	screenmover_target.top = u16[(A1+=2)-2]
	D1.u16 = u16[(A1+=2)-2]
	if (global.zone == 0x03)
	{
		D1.u16 = 0x0b70 - getScreenHeightExtend()
	}
	else if (D1.u16 != 0x1000 && D1.u16 != 0x0800)	// exclude values for no lower boundary or vertical looping
	{
		D1.u16 -= getScreenHeightExtend()
	}
	screenmover_target.bottom = D1.u16
	move_area.bottom.target = D1.u16

	// Don't call for Hydrocity, it's explicitly called there
	if (global.zone != 0x01)
	{
		fn085de0()
	}
}